buildscript {
  ext.versions = [
      'minSdk': 14,
      'compileSdk': 27,
  ]
  ext.deps = [
      'androidx': [
          'annotations': 'androidx.annotation:annotation:1.0.0',
          'test': [
              'runner': 'androidx.test:runner:1.2.0',
          ],
      ],
      'kotlinStdLib': 'org.jetbrains.kotlin:kotlin-stdlib',
      'kotlinCoroutines': 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8',
      'rxJava2': 'io.reactivex.rxjava2:rxjava:2.2.21',
      'rxJava3': 'io.reactivex.rxjava3:rxjava:3.0.3',
      'junit': 'junit:junit:4.13',
      'truth': 'com.google.truth:truth:1.0.1',
      'turbine': 'app.cash.turbine:turbine:0.1.1',
  ]

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.0.1'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.12.0'
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
    task.kotlinOptions {
      freeCompilerArgs += [
        '-progressive',
        '-XXLanguage:+NewInference',
      ]
    }
  }

  plugins.withId('com.vanniktech.maven.publish') {
    signing {
      def signingKey = findProperty('signingKey')
      def signingPassword = ''
      useInMemoryPgpKeys(signingKey, signingPassword)
    }
  }
}
