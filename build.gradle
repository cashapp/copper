buildscript {
  ext.versions = [
      'minSdk': 16,
      'compileSdk': 27,
  ]
  ext.deps = [
      'androidx': [
          'annotations': 'androidx.annotation:annotation:1.6.0',
          'test': [
              'runner': 'androidx.test:runner:1.5.2',
          ],
      ],
      'kotlinCoroutines': 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4',
      'rxJava2': 'io.reactivex.rxjava2:rxjava:2.2.21',
      'rxJava3': 'io.reactivex.rxjava3:rxjava:3.1.6',
      'junit': 'junit:junit:4.13.2',
      'truth': 'com.google.truth:truth:1.1.3',
      'turbine': 'app.cash.turbine:turbine:0.12.3',
  ]

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.4.2'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.10'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.8.10'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.12.0'
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  plugins.withId('org.jetbrains.kotlin.android') {
    kotlin {
      jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
      }
    }
  }

  plugins.withId('com.android.library') {
    android {
      compileSdkVersion versions.compileSdk

      defaultConfig {
        minSdkVersion versions.minSdk
      }

      compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
      }

      lintOptions {
        textOutput 'stdout'
        textReport true
      }
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
    task.kotlinOptions {
      freeCompilerArgs += [
        '-progressive',
      ]
    }
  }

  plugins.withId('com.vanniktech.maven.publish') {
    signing {
      def signingKey = findProperty('signingKey')
      def signingPassword = ''
      useInMemoryPgpKeys(signingKey, signingPassword)
    }
  }
}
